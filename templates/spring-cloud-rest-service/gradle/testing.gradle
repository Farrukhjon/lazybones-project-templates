configurations {
    testCompile.exclude module: 'groovy-all'
    testCompile.exclude module: 'junit-dep'
}

dependencies {
    compile 'com.jayway.jsonpath:json-path'

    testCompile "org.spockframework:spock-core:${spockVersion}",
                "org.spockframework:spock-spring:${spockVersion}",
                "org.assertj:assertj-core:${assertJVersion}",
                "info.cukes:cucumber-junit:${cucumberVersion}",
                "info.cukes:cucumber-groovy:${cucumberVersion}",
                "info.cukes:cucumber-spring:${cucumberVersion}",
                'com.jayway.jsonpath:json-path-assert',
                "com.jayway.restassured:rest-assured:${restassuredVersion}"
}

test {
    useJUnit {
        includeCategories 'org.kurron.categories.UnitTest'
    }
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
    reports.html.destination = "$buildDir/reports/unitTests"
}

task outboundIntegrationTests(type: Test, dependsOn: build) {
    group 'Verification'
    description = 'Runs the outbound integration tests.'
    useJUnit {
        includeCategories 'org.kurron.categories.OutboundIntegrationTest'
    }
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
    reports.html.destination = "$buildDir/reports/componentTests"
    // if the profile system property is set, copy into forked child process
    def springProfilesActive = System.properties['spring.profiles.active']
    if (springProfilesActive) {
        systemProperties['spring.profiles.active'] = springProfilesActive
    }
}

task inboundIntegrationTests(type: Test, dependsOn: build) {
    group 'Verification'
    description = 'Runs the inbound integration tests.'
    useJUnit {
        includeCategories 'org.kurron.categories.InboundIntegrationTest'
    }
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
    reports.html.destination = "$buildDir/reports/integrationTests"
    // if the profile system property is set, copy into forked child process
    def springProfilesActive = System.properties['spring.profiles.active']
    if (springProfilesActive) {
        systemProperties['spring.profiles.active'] = springProfilesActive
    }

    doFirst {
        // copy the integration test port that was set after the docker container was deployed
        systemProperties['integration.test.port'] = System.properties['integration.test.port']
    }
}

task acceptanceTests(type: Test, dependsOn: build) {
    group 'Verification'
    description = 'Runs the acceptance tests.'
    // categories and cucumber tests don't play nicely -- no tests are detected and run
    includes << '**/AcceptanceTests.class'
    // cucumber dictates where to place the report -- note that this will still generate an empty report at build/reports/tests
    testLogging {
        showStandardStreams = false
        exceptionFormat = 'full'
    }
    // if the profile system property is set, copy into forked child process
    def springProfilesActive = System.properties['spring.profiles.active']
    if (springProfilesActive) {
        systemProperties['spring.profiles.active'] = springProfilesActive
    }

    doFirst {
        // copy the integration test port that was set after the docker container was deployed
        systemProperties['integration.test.port'] = System.properties['integration.test.port']
    }
}


